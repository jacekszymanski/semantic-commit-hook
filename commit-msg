#!/usr/bin/env nu

let semRegex = '^(?:feat|fix|docs|style|refactor|perf|test|chore)(?:\(?(?:[A-Za-z0-9_,-]+|\s|\-|_|,)?\))?:\s\w+'
let automods = (git config --default true --get semantic-commit.auto-modules) == "true"

def checkSem [ commitMsg: string ] {
  if $commitMsg =~ $semRegex {
    return
  }
  print -e "Your commit title did not follow semantic versioning: ($commitMsg)"
  exit 1
}

def main [ commitMsg: string ] {
  if $commitMsg == "" {
    print -e "Missing argument (commit message). Did you try to run this manually?"
    exit 1
  }
  let commitTitle = (open $commitMsg | lines | first)
  if $commitTitle =~ "^Merge branch \'" {
    exit 0
  }
  checkSem $commitTitle
  if $automods and $commitTitle =~ '^(?:\w+):' {
    let mods = (
      git diff --cached --name-only
      | lines
      | where $it =~ '/'
      | str replace -a '/.*$' ''
      | str join ','
    )
    if $mods == "" {
      exit 0
    }
    let tmpfile = (mktemp)
    let replace = ($mods | str replace '(.*)' '($1):')
    let newCommitTitle = ($commitTitle | str replace  -n ':' $replace)
    $newCommitTitle | save -a $tmpfile
    open $commitMsg | lines | skip 1 | save -a $tmpfile

    mv -f $tmpfile $commitMsg
  }
}
